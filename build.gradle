buildscript {

    ext {
        joobyVersion = '1.6.2'
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath group: 'org.jooby', name: 'jooby-gradle-plugin', version: joobyVersion
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'application'
}
apply plugin: 'jooby'

ext {
    h2Version = '1.4.199'
    junitVersion = '5.4.2'
    mockitoVersion = '2.28.2'
    hamcrestVersion = '2.1'
    riderVersion = '1.7.1'
    restAssuredVersion = '3.3.0'
    javaxJsonVersion = '1.1.4'
}

group 'com.revolut'
version '1.0.0'
mainClassName = 'com.revolut.mtt.app.App'
sourceCompatibility = 1.11

repositories {
    mavenLocal()
    mavenCentral()
}

compileJava {
    options.encoding = "UTF-8"
}

test {
    useJUnitPlatform()
}

// integration tests config
sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

task integrationTest(type: Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    testLogging {
        showStandardStreams = true
        events 'started', 'passed'
        exceptionFormat = 'full'
    }
}

dependencies {

    // jooby
    compile group: 'org.jooby', name: 'jooby', version: joobyVersion
    compile group: 'org.jooby', name: 'jooby-jetty', version: joobyVersion
    compile group: 'org.jooby', name: 'jooby-jackson', version: joobyVersion
    compile group: 'org.jooby', name: 'jooby-jdbc', version: joobyVersion

    // h2
    compile group: 'com.h2database', name: 'h2', version: h2Version

    // lombok
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.8'

    // test dependencies
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
    testCompile group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
    testCompile group: 'org.mockito', name: 'mockito-junit-jupiter', version: mockitoVersion
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: hamcrestVersion
    testCompile group: 'com.github.database-rider', name: 'rider-junit5', version: riderVersion
    integrationTestCompile group: 'io.rest-assured', name: 'rest-assured', version: restAssuredVersion
    integrationTestCompile group: 'org.glassfish', name: 'javax.json', version: javaxJsonVersion
}